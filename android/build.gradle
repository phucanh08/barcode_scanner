group = 'com.anhlp.barcode_scanner'
version = '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.9.20'
    ext.protobuf_version = '0.9.5'
    ext.protocVersion = '4.30.2'
    ext.javaliteVersion = '4.30.2'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.1.4")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobuf_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "com.anhlp.barcode_scanner"
    }

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.proto.srcDirs += '../protos'
    }

    defaultConfig {
        minSdk = 24
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        if (osdetector.os == "osx") {
            artifact = "com.google.protobuf:protoc:$protocVersion:osx-x86_64"
        } else {
            artifact = "com.google.protobuf:protoc:$protocVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.protobuf:protobuf-javalite:$javaliteVersion"

    // Barcode Scanner
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    implementation 'com.google.zxing:core:3.5.3'

    // CameraX dependencies
    ext.cameraVersion = "1.4.2"
    implementation "androidx.camera:camera-core:$cameraVersion"
    implementation "androidx.camera:camera-camera2:$cameraVersion"
    implementation "androidx.camera:camera-lifecycle:$cameraVersion"
    implementation "androidx.camera:camera-view:$cameraVersion"
}
